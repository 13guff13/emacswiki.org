In many programs, like !SlickEdit, !TextMate and !VisualStudio, '''"cut" and "copy" act on the ''current line'' if no text is visually selected'''. This page lists ways to get the same behavior in Emacs, so that

* '##M-w##' copies the current line when the [[region]] is not active, and
* '##C-w##' deletes it.

'''Note:''' [TransientMarkMode Transient Mark Mode] must be enabled to distinguish selected text visually -- that is, ##(transient-mark-mode t)## -- which is the default in Emacs 23 and onwards.

== Advice ==

A concise solution is to [AdvisingFunctions advise] ##kill-ring-save## and ##kill-region## (Emacs' default copy and cut commands):

    (defadvice kill-ring-save (before slick-copy activate compile)
      "When called interactively with no active region, copy a single line instead."
      (interactive
       (if (region-active-p) (list (region-beginning) (region-end))
         (message "Copied line")
         (list (line-beginning-position)
               (line-beginning-position 2)))))

    (defadvice kill-region (before slick-cut activate compile)
      "When called interactively with no active region, kill a single line instead."
      (interactive
       (if (region-active-p) (list (region-beginning) (region-end))
         (list (line-beginning-position)
               (line-beginning-position 2)))))

Thus, `M-w' with no selection copies the current line, `C-w' kills it entirely, and `C-a M-w C-y' duplicates it. As the advice only affects the commands' ''interactive'' behavior, they are safe for other functions to call.

=== With yank-handler ==

A further keypress can be saved by using the ##yank-handler## text property. The following will paste a copied line above the current line regardless of the position of point:

    (defadvice kill-ring-save (around slick-copy activate)
      "When called interactively with no active region, copy a single line instead."
      (if (or (region-active-p) (not (called-interactively-p)))
          ad-do-it
        (kill-new (buffer-substring (line-beginning-position)
                                    (line-beginning-position 2))
                  nil '(yank-line))
        (message "Copied line")))

    (defadvice kill-region (around slick-copy activate)
      "When called interactively with no active region, kill a single line instead."
      (if (or (region-active-p) (not (called-interactively-p)))
          ad-do-it
        (kill-new (filter-buffer-substring (line-beginning-position)
                                           (line-beginning-position 2) t)
                  nil '(yank-line))))

    (defun yank-line (string)
      "Insert STRING above the current line."
      (beginning-of-line)
      (unless (= (elt string (1- (length string))) ?\n)
        (save-excursion (insert "\n")))
      (insert string))

Thus, a line can be duplicated with `M-w C-y'.

== whole-line-or-region.el ==

[http://www.northbound-train.com/emacs/whole-line-or-region.el whole-line-or-region.el] is a minor mode which allows functions to operate on the current line when they would normally operate on the region. When yanking, it's smart enough to know that the string to be yanked was killed as a whole line and should be yanked as such, so one doesn't need to position oneself at the start of the line. It also accepts prefix arguments.

Furthermore, it preserves the position of point. If killing the first line of the following text:

    The GNU Project was launched in 1984 to develop a complete
    Unix-like operating system which is free software: the GNU system.

And if point is positioned as such when yanking the killed text:

    Unix-like operating system wh>|<ich is free software: the GNU system.

One winds up with:

    The GNU Project was launched in 1984 to develop a complete
    Unix-like operating system wh>|<ich is free software: the GNU system.

In addition, the package provides a framework for enabling any region-based command (e.g., ##comment-dwim##) to work on the current line if region is undefined.

----

See also CopyingWholeLines.

----

CategoryRegion
